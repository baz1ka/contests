## [H-01] User can't deposit `ERC20` collateral token into the `RestakeManager` if `WithdrawQueue` `bufferToFill` is too large.

## Relevant GitHub Links

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/RestakeManager.sol#L543-L546

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/RestakeManager.sol#L546-L549

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/RestakeManager.sol#L562

## Impact

User can't deposit `ERC20` collateral token into the `RestakeManager` if `WithdrawQueue` isn't filled with necessary (buffer) amount of tokens. This deficit can occur when user that already has deposits withdraw their funds and withdrawal buffer deficit is not filled in time.

## Proof of Concept

To deposit `ERC20` tokens into `RestakeManager` user must call `RestakeManager.deposit()` which makes next actions:

-   Check if `WithdrawQueue` buffer needs to be filled:

```solidity
    uint256 bufferToFill = depositQueue.withdrawQueue().getBufferDeficit(
        address(_collateralToken)
    );
    if (bufferToFill > 0) {
```

-   If `bufferToFill > 0` then conditional code executes to fill withdraw buffer deficit:

```solidity
    if (bufferToFill > 0) {
        bufferToFill = (_amount <= bufferToFill) ? _amount : bufferToFill;
        // update amount to send to the operator Delegator
        _amount -= bufferToFill;

        // safe Approve for depositQueue
        _collateralToken.safeApprove(address(depositQueue), bufferToFill);

        // fill Withdraw Buffer via depositQueue
        depositQueue.fillERC20withdrawBuffer(address(_collateralToken), bufferToFill);
    }
```

-   In case `_amount <= bufferToFill` `bufferToFill` would be equal to `_amount`.
-   On next line `bufferToFill` (which is in our case equal to `_amount`) is subtracted from deposited `_amount` and in our case, it would be `0`.
-   Then this `_amount` which is `0` is deposited to `OperatorDelegator` contract:

```solidity
    // Call deposit on the operator delegator
    operatorDelegator.deposit(_collateralToken, _amount);
```

But this execution would revert as `OperatorDelegator.deposit()` doesn't accept `_amount` of `0`:

```solidity
    function deposit(
        IERC20 token,
        uint256 tokenAmount
    ) external nonReentrant onlyRestakeManager returns (uint256 shares) {
        if (address(tokenStrategyMapping[token]) == address(0x0) || tokenAmount == 0)
            revert InvalidZeroInput();

```

## Recommended Mitigation Steps

Deposit into operatorDelegator only if `_amount` is more than 0:

```diff
@@ -558,8 +558,15 @@ contract RestakeManager is Initializable, ReentrancyGuardUpgradeable, RestakeMan
        // Approve the tokens to the operator delegator
        _collateralToken.safeApprove(address(operatorDelegator), _amount);

        // Call deposit on the operator delegator
-       operatorDelegator.deposit(_collateralToken, _amount);
+       if (_amount != 0) {
+           operatorDelegator.deposit(_collateralToken, _amount);
+       }
```

## [H-02] Incorrect `RestakeManager` TVL calculation may result in calculating incorrect amounf of shares.

## Relevant GitHub Links

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/RestakeManager.sol#L318

## Summary

`RestakeManager.calculateTVLs()` calculates incorrect amount of TVL.

## Impact

Calculating totalWithdrawalQueueValue for incorrect token can lead to different TVL than it's supposed to be which may result in getting different amount of shares. Can be can be negative for the user (if price of `collateralTokens[i]` is greater than `collateralTokens[j]`) or can be negatve for the protocol (if price of `collateralTokens[i]` is smaller than `collateralTokens[j]`).

## Proof of Concept

Values returned from `RestakeManager.calculateTVLs()` are used in taking calculation of how many shares would be minted/burned to user.
However, when this function calculates value of tokens (in ETH) in `WithdrawQueue` it gets incorrect address of token to get value for.

```solidity
    // record token value of withdraw queue
    if (!withdrawQueueTokenBalanceRecorded) {
        totalWithdrawalQueueValue += renzoOracle.lookupTokenValue(
@>          collateralTokens[i],
            collateralTokens[j].balanceOf(withdrawQueue)
        );
    }
```

Instead of getting value of token in current loop iteration, it gets token value of incorrect `collateralTokens[i]`. In this case index `i` is always `0` because it's index from outer loop. That means it calculates token value of first token with amount of token it should have been calculated. `RestakeManager.calculateTVLs()` is used in `RestakeManager.deposit()`, `RestakeManager.depositETH()`, `RestakeManager.depositTokenRewardsFromProtocol()`, `BalancerRateProvider.getRate()`, `WithdrawQueue.withdraw()` which are main fund flow functions and they are dependent on correct return value from `RestakeManager.calculateTVLs()`.

## Recommended Mitigation Steps

Get collateral token from array by correct index:

```diff
@@ -315,7 +315,7 @@ contract RestakeManager is Initializable, ReentrancyGuardUpgradeable, RestakeMan
    // record token value of withdraw queue
    if (!withdrawQueueTokenBalanceRecorded) {
        totalWithdrawalQueueValue += renzoOracle.lookupTokenValue(
-           collateralTokens[i],
+           collateralTokens[j],
            collateralTokens[j].balanceOf(withdrawQueue)
        );
    }
```

## [H-03] Mistake in `OperatorDelegator.getTokenBalanceFromStrategy()` can lead to wrong calculation of TVL in protocol.

## Relevant GitHub Links

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/Delegation/OperatorDelegator.sol#L329

## Impact

Incorrect calculation of TVL can lead to minting/burning incorrect amount of shares to user.

## Proof of Concept

`OperatorDelegator.getTokenBalanceFromStrategy()` function is supposed to return

> > underlying token amount from the amount of shares + queued withdrawal shares

Condition whether to return just underlying token amount from the amount of shares or underlying token amount from the amount of shares with queued withdrawal shares is incorrect:

```solidity
    /// @dev Gets the underlying token amount from the amount of shares + queued withdrawal shares
    function getTokenBalanceFromStrategy(IERC20 token) external view returns (uint256) {
        return
@>          queuedShares[address(this)] == 0
                ? tokenStrategyMapping[token].userUnderlyingView(address(this))
                : tokenStrategyMapping[token].userUnderlyingView(address(this)) +
                    tokenStrategyMapping[token].sharesToUnderlyingView(
                        queuedShares[address(token)]
                    );
    }
```

In fact, it checks if `queuedShares` mapping has address of this contract (`OperatorDelegator`). This is wrong and doesn't make any sense as `OperatorDelegator` is not an ERC20 token and can not be deposited into strategy.

## Recommended Mitigation Steps

Correctly check whether `OperatorDelegator` has queued shares for `token` or not:

```diff
@@ -326,7 +335,7 @@ contract OperatorDelegator is
    /// @dev Gets the underlying token amount from the amount of shares + queued withdrawal shares
    function getTokenBalanceFromStrategy(IERC20 token) external view returns (uint256) {
        return
-           queuedShares[address(this)] == 0
+           queuedShares[address(token)] == 0
                ? tokenStrategyMapping[token].userUnderlyingView(address(this))
                : tokenStrategyMapping[token].userUnderlyingView(address(this)) +
                    tokenStrategyMapping[token].sharesToUnderlyingView(
```

## [H-04] Account can't refund gas in `OperatorDelegator`.

## Relevant GitHub Links

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/Delegation/OperatorDelegator.sol#L512

## Summary

`OperatorDelegator` has functionality to refund gas to the account that executed some of contract's functions. On every such function call used gas is saved in `adminGasSpentInWei` mapping which tracks how much gas was used by account. To retrieve that used gas, there is receive function which should send to admin used gas amount, but it doesn't work properly.

## Proof of Concept

Let's take a look at receive function in context when it's called by admin (not `eigenPod` contract).

```solidity
    } else {
        // considered as protocol reward
        uint256 gasRefunded = 0;
        uint256 remainingAmount = msg.value;
        if (adminGasSpentInWei[tx.origin] > 0) {
            gasRefunded = _refundGas();
            // update the remaining amount
            remainingAmount -= gasRefunded;
            // If no funds left, return
            if (remainingAmount == 0) {
                return;
            }
        }
        // Forward remaining balance to the deposit queue
        address destination = address(restakeManager.depositQueue());
        (bool success, ) = destination.call{ value: remainingAmount }("");
        if (!success) revert TransferFailed();

        emit RewardsForwarded(destination, remainingAmount);
    }
```

If admin wants just to retrieve used gas, they would send transaction with value = 0. In that case transaction would revert because of underflow when executing `remainingAmount -= gasRefunded` <=> `remainingAmount = 0 - some value that is greater than 0`. So user can not just refund their used gas. Also, user can try to provide `value` which is equal to `gasRefunded` but in that case, all `value` that user sent would just be lost (they would stuck in contract and would not be deposited into deposit queue) because of condition that checks if `remainingAmount` equals to 0.

<details>
<summary>Proof Of Code</summary>

Create .sol file for test inside `test` folder.

Place following into this file:

```solidity
    // SPDX-License-Identifier: MIT
    pragma solidity 0.8.19;

    import { Test, console } from "forge-std/Test.sol";

    import "../contracts/Delegation/OperatorDelegator.sol";

    contract MockRestakeManager {
        address public depositQueue;

        constructor(address _depositQueue) {
            depositQueue = _depositQueue;
        }
    }

    // This mock contract is needed to make it easier to fill adminGasSpentInWei without providing full data to OperatorDelegator just to speed up writing test
    contract MockOperatorDelegatoror is OperatorDelegator {
        constructor(address _restakeManager) {
            restakeManager = IRestakeManager(_restakeManager);
        }

        // this function is needed to easily make and record expensive function call
        function expensiveFunction() external {
            uint256 gasBefore = gasleft();

            uint256[] memory arr = new uint256[](200);

            for (uint256 i = 0; i < arr.length; i++) {
            arr[i] = i;
            }

            _recordGas(gasBefore);
        }
    }

    contract OperatorDelegatorTest is Test {
        MockRestakeManager restakeManager;
        MockOperatorDelegatoror operatorDelegator;

        address alice = makeAddr("alice");
        address depositQueue = makeAddr("depositQueue");

        function setUp() public {
            // deploy without proxy to make testing easier
            restakeManager = new MockRestakeManager(depositQueue);
            operatorDelegator = new MockOperatorDelegatoror(address(restakeManager));
        }

        function test_Revert_If_admin_want_to_retrieve_used_gas() public {
            uint256 operatorDelegatorBalance = 200e18;
            uint256 aliceBalance = 10e6;

            // set tx.gasprice to 1
            vm.txGasPrice(1);

            // fund OperatorDelegator with funds
            vm.deal(address(operatorDelegator), operatorDelegatorBalance);
            vm.deal(alice, aliceBalance);
            assertEq(operatorDelegatorBalance, address(operatorDelegator).balance);

            // Alice makes expensive call
            vm.prank(alice);
            operatorDelegator.expensiveFunction();

            uint256 aliceGasSpentInWei = operatorDelegator.adminGasSpentInWei(alice);

            console.log(aliceGasSpentInWei); // 39745

            // Alice want just to retrieve her funds without sending additional funds
            vm.prank(alice, alice);
            (bool success, ) = address(operatorDelegator).call("");

            console.log(success); // false, transaction failed because underflow

            // Alice try again and provide value that equals to aliceGasSpentInWei and think she can return used gas
            uint256 balanceBeforeRefund = alice.balance;

            vm.prank(alice, alice);
            (bool success1, ) = address(operatorDelegator).call{value: aliceGasSpentInWei}("");
            console.log(success1);

            uint256 balanceAfterRefund = alice.balance;

            console.log(balanceAfterRefund - balanceBeforeRefund); // 0 - no change

            // And no Alice's tokens that should'he withdrawn are sitting in OperatorDelegator without work
            // *contract send same amount of tokens to alice and toke same amount (aliceGasSpentInWei)
            // but instead should've been address(operatorDelegator).balance - aliceGasSpentInWei
            assertEq(address(operatorDelegator).balance, operatorDelegatorBalance);
        }
    }
```

Run with this test with:

```bash
    forge test --match-contract OperatorDelegatorTest --match-test test_Revert_If_admin_want_to_retrieve_used_gas -vv
```

</details>

## Recommended Mitigation Steps

Add a separate function to retrieve used gas for admin or modify existing receive function as shown below:

```diff
@@ -507,13 +515,7 @@ contract OperatorDelegator is
        uint256 gasRefunded = 0;
        uint256 remainingAmount = msg.value;
            if (adminGasSpentInWei[tx.origin] > 0) {
-               gasRefunded = _refundGas();
-               // update the remaining amount
-               remainingAmount -= gasRefunded;
-               // If no funds left, return
-               if (remainingAmount == 0) {
-                   return;
-               }
+               _refundGas();
            }
            // Forward remaining balance to the deposit queue
            address destination = address(restakeManager.depositQueue());
```

## [M-01] ETH is not redeposited in `OperatorDelegator.completeQueuedWithdrawal()`.

## Relevant GitHub Links

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/Delegation/OperatorDelegator.sol#L274-L314

## Summary

As [written](https://code4rena.com/audits/2024-04-renzo#toc-2-main-invariants) on the contest page:

> > Any rewards earned by the protocol should be redeposited (minus a protocol fee).

But this invariant is not fully implemented in `OperatorDelegator.completeQueuedWithdrawal()`. It redeposits earned ERC20 tokens but doesn't redeposit earned ETH.

```solidity
    delegationManager.completeQueuedWithdrawal(withdrawal, tokens, middlewareTimesIndex, true);

    IWithdrawQueue withdrawQueue = restakeManager.depositQueue().withdrawQueue();
    for (uint256 i; i < tokens.length; ) {
        if (address(tokens[i]) == address(0)) revert InvalidZeroInput();

        // deduct queued shares for tracking TVL
        queuedShares[address(tokens[i])] -= withdrawal.shares[i];

        // check if token is not Native ETH
@>      if (address(tokens[i]) != IS_NATIVE) {
            // Check the withdraw buffer and fill if below buffer target
            uint256 bufferToFill = withdrawQueue.getBufferDeficit(address(tokens[i]));

            // get balance of this contract
            uint256 balanceOfToken = tokens[i].balanceOf(address(this));
            if (bufferToFill > 0) {
                bufferToFill = (balanceOfToken <= bufferToFill) ? balanceOfToken : bufferToFill;

                // update amount to send to the operator Delegator
                balanceOfToken -= bufferToFill;

                // safe Approve for depositQueue
                tokens[i].safeApprove(address(restakeManager.depositQueue()), bufferToFill);

                // fill Withdraw Buffer via depositQueue
                restakeManager.depositQueue().fillERC20withdrawBuffer(
                    address(tokens[i]),
                    bufferToFill
                );
            }

            // Deposit remaining tokens back to eigenLayer
            if (balanceOfToken > 0) {
                _deposit(tokens[i], balanceOfToken);
            }
@>      } // @audit no redeposit for ETH
        unchecked {
            ++i;
        }
    }
```

As we can see in `OperatorDelegator.completeQueuedWithdrawal()` there is condition that checks whether token is ETH, and if not ERC20 tokens go to deposit flow again. But there is no code that deposits ETH.

## Recommended Mitigation Steps

Add logic to redeposit earned ETH.

_Note `DepositQueue` doesn't directly accept ETH from OperatorDelegator and in example below `OperatorDelegator` would get `ezETH`_

```diff
@@ -270,9 +271,13 @@ contract OperatorDelegator is
         uint256 gasBefore = gasleft();
         if (tokens.length != withdrawal.strategies.length) revert MismatchedArrayLengths();

+        uint256 initialbalance = address(this).balance;
+
         // complete the queued withdrawal from EigenLayer with receiveAsToken set to true
         delegationManager.completeQueuedWithdrawal(withdrawal, tokens, middlewareTimesIndex, true);

+        uint256 amountToDeposit = address(this).balance - initialbalance;
+
         IWithdrawQueue withdrawQueue = restakeManager.depositQueue().withdrawQueue();
         for (uint256 i; i < tokens.length; ) {
             if (address(tokens[i]) == address(0)) revert InvalidZeroInput();
@@ -307,7 +312,10 @@ contract OperatorDelegator is
                 if (balanceOfToken > 0) {
                     _deposit(tokens[i], balanceOfToken);
                 }
+            } else {
+                restakeManager.depositETH{value: amountToDeposit}();
             }
+
             unchecked {
                 ++i;
             }
```

## [M-02] `RestakeManager.deposit()` can emit `Deposit` with wrong `_amount` argument value.

## Relevant GitHub Links

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/RestakeManager.sol#L549

## Summary

After successfully executing `RestakeManager.deposit()` `Deposit` event is emmited. It accepts few parameters and one of them is the amount user has deposited.

As the sponsor said in discord

> > renzo has subgraph which listens to those events

But `amount` may have smaller value than it should've been. Below is the code where the `_amount` value can be changed.

```solidity
    uint256 bufferToFill = depositQueue.withdrawQueue().getBufferDeficit(
        address(_collateralToken)
    );
    if (bufferToFill > 0) {
        bufferToFill = (_amount <= bufferToFill) ? _amount : bufferToFill;
        // update amount to send to the operator Delegator
        _amount -= bufferToFill;

        // safe Approve for depositQueue
        _collateralToken.safeApprove(address(depositQueue), bufferToFill);

        // fill Withdraw Buffer via depositQueue
        depositQueue.fillERC20withdrawBuffer(address(_collateralToken), bufferToFill);
    }
```

If `WithdrawQueue` have buffer deficit this code block would be executed. It checks how much user can fill buffer and subtracts this value from initial deposit (`_amount`). Then the remaining value is used to deposit to `OperatorDelegator`. But when event emmits, it has `_amount` value from which `bufferToFill` is substracted which is incorrect as user deposited `_amount` value, not `_amount -= bufferToFill`.

## Impact

Impact can vary depending on what is taken into account when listening to this event, but if it depends on `_amount` parameter then there can be problems related to this.

## Recommended Mitigation Steps

Consider adding `bufferToFill` to `_amount` when emitting `Deposit` event:

```diff
@@ -572,7 +572,7 @@ contract RestakeManager is Initializable, ReentrancyGuardUpgradeable, RestakeMan
        ezETH.mint(msg.sender, ezETHToMint);

        // Emit the deposit event
-       emit Deposit(msg.sender, _collateralToken, _amount, ezETHToMint, _referralId);
+       emit Deposit(msg.sender, _collateralToken, _amount + bufferToFill, ezETHToMint, _referralId);
    }
```

## [M-03] `ERC165` is not fully implemented in `TimelockController`.

## Relevant GitHub Links

https://github.com/code-423n4/2024-04-renzo/blob/519e518f2d8dec9acf6482b84a181e403070d22d/contracts/TimelockController.sol#L145-L147

## Summary

`TimelockController` implements interfaces of `IERC1155Receiver` and `IERC721Receiver` which are used to retrieve `ERC1155` and `ERC721` tokens as contracts are required to implement these interfaces to make transfer successful. But function `TimelockController.supportsInterface()` doesn't check if `interfaceId` belongs to `IERC721Receiver` interface.

## Recommended Mitigation Steps

Add check for `IERC721Receiver` support:

```diff
@@ -143,7 +143,8 @@ contract TimelockController is AccessControl, IERC721Receiver, IERC1155Receiver
         bytes4 interfaceId
     ) public view virtual override(IERC165, AccessControl) returns (bool) {
         return
-            interfaceId == type(IERC1155Receiver).interfaceId ||
+            interfaceId == type(IERC1155Receiver).interfaceId ||
+            interfaceId == type(IERC721Receiver).interfaceId ||
             super.supportsInterface(interfaceId);
     }
```
